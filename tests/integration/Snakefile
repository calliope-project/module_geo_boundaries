# Emulate a user configuring the module.
configfile: workflow.source_path("./test_config.yaml")

# Import the module and configure it.
# `snakefile:` can use plain paths, URLs or special github(...) / gitlab(...) markers.
# `prefix:` re-routes all input/output paths of the module, helping to avoid file conflicts.
module geo_boundaries_module:
    snakefile: "../../workflow/Snakefile"
    config: config["geo_boundaries_module"]
    prefix: "results/geo_boundaries_module/"

# Add a prefix to all module rules, to avoid naming conflicts.
use rule * from geo_boundaries_module as geo_boundaries_module_*

# Request something from the module
rule all:
    message: "A generic test case for this module."
    default_target: True
    input:
        "results/geo_boundaries_module/results/shapes.parquet"
